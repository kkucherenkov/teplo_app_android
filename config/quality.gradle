apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

check.dependsOn 'findbugs', 'pmd'

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/quality/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/quality/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    showViolations false
    ignoreFailures = true
    classpath = files()

    reports {
        xml.enabled = true
        html.enabled = true
        xml {
            destination "$project.buildDir/outputs/reports/checkstyle/checkstyle.xml"
        }
        html {
            destination "$project.buildDir/outputs/reports/checkstyle/checkstyle.html"
        }
    }
}

task findbugs(type: FindBugs, dependsOn: assemble) {
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("${project.rootDir}/config/quality/findbugs/findbugs-filter.xml")
    classes = fileTree("${project.rootDir}/app/build/intermediates/classes/")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "$project.buildDir/outputs/reports/findbugs/findbugs.xml"
        }
        html {
            destination "$project.buildDir/outputs/reports/findbugs/findbugs.html"
        }
    }

    classpath = files()
}

task pmd(type: Pmd) {
    ignoreFailures = true
    ruleSetFiles = files("${project.rootDir}/config/quality/pmd/pmd-ruleset.xml")
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        html.enabled = true
        xml {
            destination "$project.buildDir/outputs/reports/pmd/pmd.xml"
        }
        html {
            destination "$project.buildDir/outputs/reports/pmd/pmd.html"
        }
    }
}

android {
    lintOptions {
        abortOnError false
        xmlReport false
        htmlReport true
        lintConfig file("${project.rootDir}/config/quality/lint/lint.xml")
        htmlOutput file("$project.buildDir/outputs/reports/lint/lint-result.html")
        xmlOutput file("$project.buildDir/outputs/reports/lint/lint-result.xml")
    }
}