apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply from: '../config/quality.gradle'
/**
 * Use the number of seconds/10 since Jan 1 2016 as the versionCode.
 * This lets us upload a new build at most every 10 seconds for the
 * next 680 years.
 */
def vcode = (int) (((new Date().getTime() / 1000) - 1451606400) / 10)

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    def versionMajor = 0
    def versionMinor = 5
    def versionBuild
    if (System.getenv()["CI"]) {
        if (System.getenv()["GREENHOUSE"]) {
            versionBuild = System.getenv()["GREENHOUSE_BUILD_NUMBER"].toInteger()
        }
        if (System.getenv()["CIRCLECI"]) {
            versionBuild = System.getenv()["CIRCLE_BUILD_NUM"].toInteger()
        }
    } else {
        versionBuild = 3
    }

    defaultConfig {
        manifestPlaceholders = [HOCKEYAPP_APP_ID: "d209dce7add34db8b684c39b995c84fc"]
        applicationId "com.kkucherenkov.teploapp"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode vcode
        versionName "${versionMajor}.${versionMinor}.${versionBuild} Dev"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile('eu.livotov.labs.android:CAMView:2.0.1@aar') { transitive = true }
    compile 'com.google.code.gson:gson:2.4'
    compile 'net.hockeyapp.android:HockeySDK:4.1.0'

    compile 'io.reactivex:rxandroid:1.2.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.6'

    compile 'com.jakewharton:butterknife:8.4.0'
    compile 'com.google.dagger:dagger:2.0.2'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:support-v4:24.2.1'
    testCompile 'junit:junit:4.12'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'javax.annotation:jsr250-api:1.0'
}
